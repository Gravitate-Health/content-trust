plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    maven {
        url 'https://maven.ee.guardtime.com/nexus/content/repositories/releases/'
        mavenContent { releasesOnly() }
    }
}

dependencies {
    implementation project(path: ':ct-signer')
    implementation project(path: ':ct-verifier')
    implementation 'com.guardtime.resonance:ksi-spring-boot-starter:0.4.0'
    implementation libs.ksi.blocksigner

    implementation 'io.github.erdtman:java-json-canonicalization:1.1'
    implementation libs.jackson.databind
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.keycloak:keycloak-core:22.0.3'
    implementation 'org.keycloak:keycloak-policy-enforcer:22.0.3'

    errorprone libs.error.prone.core
}

testing {
    suites {
        test {
            testType = TestSuiteType.UNIT_TEST
            dependencies {
                implementation libs.junit.jupiter.api
                implementation libs.mockito.core
                runtimeOnly libs.junit.jupiter.engine
            }
        }
        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            dependencies {
                implementation project()
                implementation 'io.rest-assured:rest-assured:5.3.2'
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation 'org.testcontainers:junit-jupiter:1.19.1'
                implementation 'org.testcontainers:testcontainers:1.19.1'
                implementation 'com.github.dasniko:testcontainers-keycloak:3.0.0'
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
    dependsOn testing.suites.integrationTest
}
